-- Note: All grammar labels and productions are prefixed with "G" in order to
-- differentiate them from types in the target language with the same name

-- tokens

token UIdent  (upper (letter | digit | '_')*) ;
token LIdent  (lower (letter | digit | '_')*) ;
token IntTok  ('~'? digit+) ;

-- Sequential language

GUnit.       GUnit ::= "{}" ;

GTInt.       GType ::= "Int" ;
GTBool.      GType ::= "Bool" ;
GTUnit.      GType ::=  GUnit ;

GTTupFieldName. GTTupFieldName ::= LIdent ;

-- Single element list terminated with ::
(:[]). [GTTupFieldName] ::= GTTupFieldName "::" ;

-- Tuple element is one GTTupFieldName followed by type
GTTupElement. GTTupElement ::= [GTTupFieldName] GType ;
separator     nonempty GTTupElement "," ;

GTTupDef.    GTTupDef ::= "{" [GTTupElement] "}" ;
GTTupTyName. GTTupTyName ::= UIdent ;

GTTup.       GType ::= "stuct" GTTupTyName GTTupDef ;

GELetIdent.  GELetIdent ::= LIdent "=" GExpr ;
terminator   GELetIdent ";" ;

GELetExpr.   GELetExpr ::= [GELetIdent] GExpr ;

GEAppArg.    GEAppArg ::= GExpr ;
separator    nonempty GEAppArg "," ;

GEAbsArg.    GEAbsArg ::= LIdent ;
separator    nonempty GEAbsArg "" ;

-- tuple args can be empty (empty tuple)
GETupArg.    GETupArg ::= GExpr ;
separator    GETupArg "," ;

GELeftApp.   GExpr0 ::= GExpr1 "<$" GExpr0 ;
GERightApp.  GExpr0 ::= GExpr0 "$>" GExpr1 ;

--GEBind.      GExpr1 ::= GExpr1 ">>=" GExpr2 ;
--GESeq.       GExpr1 ::= GExpr1 ">>"  GExpr2 ;

GELogOr.     GExpr2 ::= GExpr3 "||" GExpr2 ;

GELogAnd.    GExpr3 ::= GExpr4 "&&" GExpr3 ;

GEEq.        GExpr4 ::= GExpr4 "==" GExpr4 ;
GENotEq.     GExpr4 ::= GExpr4 "!=" GExpr4 ;
GEGtEq.      GExpr4 ::= GExpr4 ">=" GExpr4 ;
GELtEq.      GExpr4 ::= GExpr4 "<=" GExpr4 ;
GEGt.        GExpr4 ::= GExpr4 ">"  GExpr4 ;
GELt.        GExpr4 ::= GExpr4 "<"  GExpr4 ;

GEPlus.      GExpr6 ::= GExpr6 "+" GExpr7 ;
GEMinus.     GExpr6 ::= GExpr6 "-" GExpr7 ;

GETimes.     GExpr7 ::= GExpr7 "*" GExpr8 ;
GEDiv.       GExpr7 ::= GExpr7 "/" GExpr8 ;
GEMod.       GExpr7 ::= GExpr7 "%" GExpr8 ;

GELeftComp.  GExpr9 ::= GExpr10 "<o" GExpr9 ;
GERightComp. GExpr9 ::= GExpr9  "o>" GExpr10 ;

GELogNot.    GExpr10 ::= "!" GExpr10 ;

GEIdx.       GExpr11 ::= GExpr11 "::" GExpr12 ;

GEIf.        GExpr12 ::= "if" GExpr "{" GELetExpr "}" "else" "{" GELetExpr "}" ;

GEApp.       GExpr13 ::= GExpr13 "(" [GEAppArg] ")" ;

GEAbs.       GExpr14 ::= "\\" [GEAbsArg] "->" "{" GELetExpr "}" ;

GEVar.       GExpr15 ::= LIdent ;
GEInt.       GExpr15 ::= IntTok ;
GETrue.      GExpr15 ::= "True" ;
GEFalse.     GExpr15 ::= "False" ;
GETup.       GExpr15 ::= "{" [GETupArg] "}" ;
GEUnit.      GExpr15 ::= GUnit ;

coercions    GExpr 15 ;

-- Hardware language

GHwArea.    GHwArea ::= "area" ;
terminator  GHwArea "" ;

rules GHwStateType ::= GUnit | GTTupTyName | GTTupDef ;
GHwState.   GHwState ::= "state" GHwStateType ;

GHwChan.    GHwChan ::= "channel" ;
terminator  GHwChan ";" ;

GHwInit.    GHwInit ::= "init" ;
GHwHandler. GHwHandler ::= "handler" ;

-- Combine the three

GFunc.      GFunc ::= "func" Ident ;
GProc.      GProc ::= "proc" Ident ;
GHwproc.    GHwproc ::= "hwproc" Ident "{" [GHwArea] GHwState [GHwChan] GHwInit GHwHandler "}" ;

rules GTopDef ::= GFunc | GProc | GHwproc ;
terminator  GTopDef "" ;

GProg.      GProg ::= [GTopDef] ;

comment "//" ;

-- Only export nessecary top level parsers
entrypoints GProg, GFunc, GProc, GHwproc, GExpr ;

